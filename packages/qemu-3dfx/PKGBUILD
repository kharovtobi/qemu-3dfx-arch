# Courtesy from kharovtobi <https://github.com/kharovtobi>
# Most of the repository source code is not my work! it belongs to kjliew at https://github.com/kjliew/qemu-3dfx (pls support him)
# Do not post this into AUR!
# The PKGBUILD is dependent on upstream repository and uses his patch..
pkgname=qemu-3dfx
pkgver=8.2.1
pkgrel=4
pkgdesc="MESA GL/3Dfx Glide pass-through for QEMU"
arch=("x86_64")
url="https://github.com/kjliew/qemu-3dfx"
license=('GPL-2.0')
depends=("qemu-base" "seabios")
makedepends=(
    "alsa-lib"
    "brltty"
    "bzip2"
    "cairo"
    "capstone"
    "cdrtools"
    "coreutils"
    "curl"
    "dos2unix"
    "dtc"
    "fuse3"
    "gcc-libs"
    "gdk-pixbuf2"
    "git"
    "glib2"
    "glibc"
    "glib2-devel"
    "glusterfs"
    "gnutls"
    "gtk3"
    "jack"
    "keyutils"
    "libaio"
    "libbpf"
    "libcacard"
    "libcap-ng"
    "libepoxy"
    "libiscsi"
    "libnfs"
    "libpipewire"
    "libpng"
    "libpulse"
    "libsasl"
    "libseccomp"
    "libslirp"
    "libssh"
    "liburing"
    "libusb"
    "libx11"
    "libxdp"
    "libxkbcommon"
    "libxml2"
    "lzo"
    "mesa"
    "meson"
    "multipath-tools"
    "mingw-w64-binutils"
    "mingw-w64-gcc"
    "mingw-w64-tools"
    "ncurses"
    "ninja"
    "ndctl"
    "numactl"
    "patch"
    "pam"
    "pcre2"
    "python"
    "python-setuptools"
    "python-distlib"
    "python-pip"
    "python-sphinx"
    "python-sphinx_rtd_theme"
    "pixman"
    "rsync"
    "sdl2"
    "sdl2_image"
    "snappy"
    "spice"
    "spice-protocol"
    "systemd"
    "usbredir"
    "vde2"
    "virglrenderer"
    "vte3"
    "xxd"
    "zlib"
    "zstd"
)
optdepends=(
    "qemu-docs: for documentation and learning invocation"
    "openglide-3dfx: host glide wrappers"
)
provides=("qemu-3dfx")
conflicts=("qemu-3dfx")
source=(
    "git+https://github.com/kjliew/qemu-3dfx.git"
    "https://download.qemu.org/qemu-${pkgver}.tar.xz"
    "https://github.com/andrewwutw/build-djgpp/releases/download/v3.4/djgpp-linux64-gcc1220.tar.bz2"
    "https://github.com/open-watcom/open-watcom-v2/releases/download/2024-12-02-Build/ow-snapshot.tar.xz"
)
noextract=("qemu-${pkgver}.tar.xz" "djgpp-linux64-gcc1220.tar.bz2" "ow-snapshot.tar.xz")
sha256sums=(
    'SKIP'
    '8562751158175f9d187c5f22b57555abe3c870f0325c8ced12c34c6d987729be'
    '8464f17017d6ab1b2bb2df4ed82357b5bf692e6e2b7fee37e315638f3d505f00'
    '9548cb62bd84caaeaacaf6a2a282c9322e77f4d0610869f87c557815a327a38d'
)
prepare() {
    rm -rf "$pkgname"/watcom
    mkdir "$srcdir"/"$pkgname"/watcom
    tar xf qemu-${pkgver}.tar.xz -C "$pkgname"
    tar xf djgpp-linux64-gcc1220.tar.bz2 -C "$pkgname"
    cp  "$srcdir"/"$pkgname"/djgpp/i586-pc-msdosdjgpp/bin/dxe* "$srcdir"/"$pkgname"/djgpp/bin
    tar xf ow-snapshot.tar.xz --directory "$pkgname"/watcom
    cd "$pkgname"/qemu-${pkgver}
    rsync -r ../qemu-0/hw/3dfx ../qemu-1/hw/mesa ./hw/
    patch -p0 -i  ../00-qemu82x-mesa-glide.patch
    bash ../scripts/sign_commit
    rm -rf "$srcdir"/"$pkgname"/build
    mkdir -p "$srcdir"/"$pkgname"/build
}
build() {
    cd "$srcdir"/"$pkgname"/build
    ../qemu-${pkgver}/configure --target-list="i386-softmmu" --prefix=/usr --disable-xen --disable-libnfs --extra-cflags="-march=native -mtune=native -O3 -flto=auto"
    make clean
    make qemu-system-i386

    export WATCOM="$srcdir"/"$pkgname"/watcom
    export PATH=$WATCOM/binl64:$WATCOM/binl:$PATH
    export EDPATH=$WATCOM/eddat
    export INCLUDE=$WATCOM/lh
    export PATH="$srcdir"/"$pkgname"/djgpp/bin:$PATH
    cd "$srcdir"/"$pkgname"/wrappers/3dfx
    rm -rf build && mkdir build && cd build
    bash ../../../scripts/conf_wrapper
    make && make clean
    cd ../../mesa
    rm -rf build && mkdir build && cd build
    bash ../../../scripts/conf_wrapper
    make && make clean
    cd ../../
    rm -rf "$srcdir"/"$pkgname"/wrappers/iso
    mkdir iso && cd iso
    mkdir wrapfx && mkdir wrapgl
    cp -r ../3dfx/build/* ./wrapfx/
    rm -r ./wrapfx/lib* ./wrapfx/Makefile
    cp -r ../mesa/build/* ./wrapgl/
    cp -r ../../LICENSE license.txt
    rm -r ./wrapgl/Makefile
    echo $(git rev-parse HEAD) > commit\ id.txt
    unix2dos commit\ id.txt license.txt
    mkisofs -o ../wrappers.iso ../iso
}
package() {
    install -Dm644 "$srcdir"/"$pkgname"/LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
    install -Dm644 "$srcdir"/"$pkgname"/wrappers/wrappers.iso "$pkgdir"/usr/share/"$pkgname"/wrappers.iso
    install -Dm755 "$srcdir"/"$pkgname"/build/qemu-system-i386 "$pkgdir"/usr/bin/qemu-3dfx-system-i386
    mkdir -p "$pkgdir"/usr/share/man/man1/
    ln -sf /usr/share/man/man1/qemu.1.gz "$pkgdir"/usr/share/man/man1/qemu-3dfx-system-i386.1.gz
    msg Copy\ the\ wrapper\ disk\ at\ /usr/share/qemu-3dfx\ to\ your\ home\ dir\ and\ run\ qemu-3dfx-system-i386!
}
